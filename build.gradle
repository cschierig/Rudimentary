plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "rudimentary" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}
fabricApi {
    configureDataGeneration {
        modId = "rudimentary"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    //modCompileOnly "maven.modrinth:immersive-crafting:${project.immersive_crafting_version}:api"
    modImplementation "maven.modrinth:immersive-crafting:${project.immersive_crafting_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName}" }
    }
}

if (System.getenv("MODRINTH_TOKEN") != null) {
    modrinth {
        token.set(System.getenv("MODRINTH_TOKEN"))
        projectId = "rudimentary"
        versionNumber.set(project.version.toString())
        versionName.set(project.version.toString())
        versionType.set("beta")
        uploadFile.set(tasks.named("remapJar"))
        syncBodyFrom = rootProject.file("README.md").text
        dependencies {
            required.project("fabric-api")
        }
        gameVersions = [project.minecraft_version]
        loaders = ["fabric", "quilt"]
        detectLoaders.set(false)
        changelog = file("CHANGELOG.md").text
    }
}

tasks.register('release') {
    dependsOn(":modrinth")
    dependsOn(":modrinthSyncBody")
}